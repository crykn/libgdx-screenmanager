apply plugin: "java-library"
apply plugin: "eclipse"
apply plugin: "idea"

version '0.7.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (JavaVersion.current().isJava9Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption("-release", "8");
    }
}

repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
}

// PUBLISH SOURCES & JAVADOC
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}	

// EXAMPLES
sourceSets {
    example {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
        }
    }
}

// DEPENDENCIES
ext {
	gdxVersion = "1.12.1"
	guacamoleVersion = "92d161f06c" // =v.0.3.6
	
	// Test dependencies
	junitVersion = "5.10.0"
	mockitoVersion = "4.11.0"
}

dependencies {
	implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
	api "com.github.crykn.guacamole:gdx:$guacamoleVersion" // is exposed because of NestableFrameBuffer
	
	testImplementation "org.junit.platform:junit-platform-launcher:1.2.0" // needed for a bug in older eclipse versions
	testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testImplementation "org.mockito:mockito-inline:$mockitoVersion"
	testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
	testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
	testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
	
	exampleImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	exampleImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}


// TESTS
test {
	useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

// Clearing Eclipse project data in root folder
tasks.eclipse.doLast {
	delete '.project'
	delete '.classpath'
	delete '.settings/'
}
